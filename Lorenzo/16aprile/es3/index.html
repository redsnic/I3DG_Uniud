<!DOCTYPE html>
<html>
	<head>
        <meta charset=utf-8>
        <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<title></title>
		<style>
			body { margin: 0; }
			canvas { width: 100%; height: 100% }
		</style>
	</head>
	<body>
    <script src="../../lib/stats.js"></script>
	<script src="../../lib/three.js"></script>
	<script src="../../lib/OrbitControls.js"></script>
    <script src="../../lib/dat.gui.js"></script>
    
    <script type="text/x-glsl" id="vertex">

		// the semplification allows to remove the numator in G1 and remove the denominator (4*nl*nv) becomes 4

		uniform vec3 pointLightPosition; // in world space
		uniform vec3 clight;
		uniform vec3 cspec;
		uniform float roughness;
		const float PI = 3.14159;
		varying vec4 fragColor;

		// Fresnel reflectance (input is the precomputed dot product)
		vec3 FSchlick(float lDoth) {
			return (cspec + (vec3(1.0)-cspec)*pow(1.0 - lDoth,5.0));
		}

		// normal distribution function
		float DGGX(float nDoth, float alpha) {
			float alpha2 = alpha*alpha;
			float d = nDoth*nDoth*(alpha2-1.0)+1.0;
			return (  alpha2 / (PI*d*d));
		}

		float G1(float dotProduct, float k) {
			return (1.0 / (dotProduct*(1.0-k) + k) );
		}

		// geometric factor
		float GSmith(float nDotv, float nDotl) {
			float k = roughness*roughness;
			return G1(nDotl,k)*G1(nDotv,k);
		}

		void main() {
			vec4 vPos = modelViewMatrix * vec4( position, 1.0 );
			vec3 vPosition = vPos.xyz;
			vec3 vNormal = normalMatrix * normal;
			gl_Position = projectionMatrix * vPos;

			vec4 lPosition = viewMatrix * vec4( pointLightPosition, 1.0 );
			vec3 l = normalize(lPosition.xyz - vPosition.xyz);
			vec3 n = normalize( vNormal );  // interpolation destroys normalization, so we have to normalize
			vec3 v = normalize( -vPosition);
			vec3 h = normalize( v + l);
			// small quantity to prevent divisions by 0
			float nDotl = max(dot( n, l ),0.000001);
			float lDoth = max(dot( l, h ),0.000001);
			float nDoth = max(dot( n, h ),0.000001);
			float vDoth = max(dot( v, h ),0.000001);
			float nDotv = max(dot( n, v ),0.000001);
			vec3 specularBRDF = FSchlick(lDoth)*GSmith(nDotv,nDotl)*DGGX(nDoth,roughness*roughness)/4.0;
			vec3 outRadiance = PI* clight * nDotl * specularBRDF;

			fragColor = vec4(pow( outRadiance, vec3(1.0/2.2)), 1.0);
		}
	</script>

	<script type="text/x-glsl" id="fragment">
		varying vec4 fragColor;

		void main() {
			gl_FragColor = fragColor;
			// commento: l'effetto appare migliore eseguendo le operazioni nel fragment shader
		}
    </script>
    
    <script src="es3.js"></script>
	</body>
</html>