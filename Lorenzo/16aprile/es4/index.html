<!DOCTYPE html>
<html>
	<head>
        <meta charset=utf-8>
        <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<title></title>
		<style>
			body { margin: 0; }
			canvas { width: 100%; height: 100% }
		</style>
	</head>
	<body>
    <script src="../../lib/stats.js"></script>
	<script src="../../lib/three.js"></script>
	<script src="../../lib/OrbitControls.js"></script>
    <script src="../../lib/dat.gui.js"></script>
    
    <script type="text/x-glsl" id="vertex">
		varying vec3 vNormal;
		varying vec3 vPosition;

		void main() {
			vec4 vPos = modelViewMatrix * vec4( position, 1.0 );
			vPosition = vPos.xyz;
			vNormal = normalMatrix * normal;
			gl_Position = projectionMatrix * vPos;
		}
	</script>

	<script type="text/x-glsl" id="fragment">
            varying vec3 vNormal;
			varying vec3 vPosition;
			uniform vec3 pointLightPosition; // in world space
			uniform vec3 clight;
			uniform vec3 material_color;
			uniform float metalness;
			uniform float roughness;
			const float PI = 3.14159;
			
			vec3 cdiff;
			vec3 cspec;
			
			           
			float mapLinear(float value, float sourceMin, float sourceMax, float destMin, float destMax ) {
                float result = destMin + (( value - sourceMin ) * ( destMax - destMin )) / ( sourceMax - sourceMin );
                return result;
            }
            
            void set_cdiff_cspec() {
                cdiff = vec3(0.0);
                cspec = vec3(0.0);
			
                cdiff.r = max( material_color.r - metalness, 0.0 );
                cdiff.g = max( material_color.g - metalness, 0.0 );
                cdiff.b = max( material_color.b - metalness, 0.0 );
                
                cspec.r = mapLinear( material_color.r * metalness, 0.0, 1.0, 0.04, 1.0 );
                cspec.g = mapLinear( material_color.g * metalness, 0.0, 1.0, 0.04, 1.0 );
                cspec.b = mapLinear( material_color.b * metalness, 0.0, 1.0, 0.04, 1.0 );
            }

			vec3 FSchlick(float lDoth) {
				return (cspec + (vec3(1.0)-cspec)*pow(1.0 - lDoth,5.0));
			}

			float DGGX(float nDoth, float alpha) {
				float alpha2 = alpha*alpha;
				float d = nDoth*nDoth*(alpha2-1.0)+1.0;
				return (  alpha2 / (PI*d*d));
			}

			float G1(float dotProduct, float k) {
				return (dotProduct / (dotProduct*(1.0-k) + k) );
			}

			float GSmith(float nDotv, float nDotl) {
					float k = roughness*roughness;
					return G1(nDotl,k)*G1(nDotv,k);
			}

			void main() {
			
                set_cdiff_cspec();
                
				vec4 lPosition = viewMatrix * vec4( pointLightPosition, 1.0 );
				vec3 l = normalize(lPosition.xyz - vPosition.xyz);
				vec3 n = normalize( vNormal );  // interpolation destroys normalization, so we have to normalize
				vec3 v = normalize( -vPosition);
				vec3 h = normalize( v + l);
				// small quantity to prevent divisions by 0
				float nDotl = max(dot( n, l ),0.000001);
				float lDoth = max(dot( l, h ),0.000001);
				float nDoth = max(dot( n, h ),0.000001);
				float vDoth = max(dot( v, h ),0.000001);
				float nDotv = max(dot( n, v ),0.000001);
				vec3 fresnel = FSchlick(lDoth);
				vec3 BRDF = (vec3(1.0)-fresnel)*cdiff/PI + fresnel*GSmith(nDotv,nDotl)*DGGX(nDoth,roughness*roughness)/
					(4.0*nDotl*nDotv);
				vec3 outRadiance = PI* clight * nDotl * BRDF;
				gl_FragColor = vec4(pow( outRadiance, vec3(1.0/2.2)), 1.0);
			}
    </script>
    
    <script src="es4.js"></script>
	</body>
</html>
