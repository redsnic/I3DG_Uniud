 
<!DOCTYPE html>
<html lang="en">
	<head>
		<title>three.js webgl - normal mapping</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<style>
			body {
				background-color: #f0f0f0;
				margin: 0px;
				overflow: hidden;
			}
		</style>
	</head>
	<body>

		<script src="../../lib/three.js"></script>
		<script src="../../lib/stats.js"></script>
		<script src="../../lib/OrbitControls.js"></script>
		<script src='../../lib/dat.gui.js'></script>
		<script src='../../lib/LoaderSupport.js'></script>
		<script src='../../lib/OBJLoader2.js'></script>
		<script src='../../lib/BufferGeometryUtils.js'></script>

		<script type="text/x-glsl" id="vertex">
		attribute vec4 tangent;
		varying vec3 vNormal;
		varying vec3 vPosition;
		varying vec2 vUv;
		varying vec3 vTangent;
		varying vec3 vBitangent;

		void main() {
			vec4 vPos = modelViewMatrix * vec4( position, 1.0 );
			vPosition = vPos.xyz;
			vNormal = normalize(normalMatrix * normal);
			vec3 objectTangent = vec3( tangent.xyz );
			vec3 transformedTangent = normalMatrix * objectTangent;
			vTangent = normalize( transformedTangent );
			vBitangent = normalize( cross( vNormal, vTangent ) * tangent.w );
			vUv = uv;
			gl_Position = projectionMatrix * vPos;
		}
		</script>

		<script type="text/x-glsl" id="fragment">
			varying vec3 vNormal;
			varying vec3 vTangent;
			varying vec3 vBitangent;
			varying vec3 vPosition;
			varying vec2 vUv;
			uniform vec3 pointLightPosition; // in world space
			uniform vec3 clight;
			uniform vec3 cspec;
			uniform vec3 cdiff;
			uniform float roughness;
			uniform sampler2D normalMap;
			uniform vec2 normalScale;
			const float PI = 3.14159;

			vec3 FSchlick(float lDoth) {
				return (cspec + (vec3(1.0)-cspec)*pow(1.0 - lDoth,5.0));
			}

			float DGGX(float nDoth, float alpha) {
				float alpha2 = alpha*alpha;
				float d = nDoth*nDoth*(alpha2-1.0)+1.0;
				return (  alpha2 / (PI*d*d));
			}

			float G1(float dotProduct, float k) {
				return (dotProduct / (dotProduct*(1.0-k) + k) );
			}

			float GSmith(float nDotv, float nDotl) {
					float k = roughness*roughness;
					return G1(nDotl,k)*G1(nDotv,k);
			}


			void main() {
				vec4 lPosition = viewMatrix * vec4( pointLightPosition, 1.0 );
				vec3 l = normalize(lPosition.xyz - vPosition.xyz);
				vec3 normal = normalize( vNormal );
				vec3 tangent = normalize( vTangent );
				vec3 bitangent = normalize( vBitangent );
				mat3 vTBN = mat3( tangent, bitangent, normal );
				// decodifico la normale codificata come colore nella texture
				vec3 mapN = texture2D( normalMap, vUv ).xyz * 2.0 - 1.0;
				mapN.xy = normalScale * mapN.xy;
				vec3 n = normalize( vTBN * mapN );
				vec3 v = normalize( -vPosition);
				vec3 h = normalize( v + l);
				// small quantity to prevent divisions by 0
				float nDotl = max(dot( n, l ),0.000001);
				float lDoth = max(dot( l, h ),0.000001);
				float nDoth = max(dot( n, h ),0.000001);
				float vDoth = max(dot( v, h ),0.000001);
				float nDotv = max(dot( n, v ),0.000001);
				vec3 fresnel = FSchlick(lDoth);
				vec3 BRDF = (vec3(1.0)-fresnel)*cdiff/PI + fresnel*GSmith(nDotv,nDotl)*DGGX(nDoth,roughness*roughness)/
					(4.0*nDotl*nDotv);
				vec3 outRadiance = PI* clight * nDotl * BRDF;
				// gamma encode the final value
				gl_FragColor = vec4(pow( outRadiance, vec3(1.0/2.2)), 1.0);
			}
		</script>
        <script src="esempio1.js"></script>
	</body>
</html>