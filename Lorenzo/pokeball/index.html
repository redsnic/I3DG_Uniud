
<html>
<head>
    <meta charset=utf-8>
    <title>Pokeball customizer</title>
        <style>
            body {
                margin: 0;
            }
            canvas {
                width: 100%;
                height: 100%
            }
        </style>
</head>

<body>
    <script src="../lib/three.js"></script>
    <script src="../lib/stats.js"></script>
    <script src="../lib/OrbitControls.js"></script>
    <script src="../lib/LoaderSupport.js"></script>
    <script src="../lib/OBJLoader2.js"></script>
    <script src="../lib/BufferGeometryUtils.js"></script>

    <script type="text/x-glsl" id="vertex">
        attribute vec4 tangent;
        varying vec2 vertexUV;
        varying vec3 vNormal;
		varying vec3 vPosition;
        varying vec3 vTangent;
		varying vec3 vBitangent;

		void main() {
            vec4 vPos = modelViewMatrix * vec4( position, 1.0 );
            vPosition = vPos.xyz;
            vNormal = normalMatrix * normal;
            vec3 objectTangent = vec3( tangent.xyz );
			vec3 transformedTangent = normalMatrix * objectTangent;
			vTangent = normalize( transformedTangent );
			vBitangent = normalize( cross( vNormal, vTangent ) * tangent.w );
            vertexUV = uv;
            gl_Position = projectionMatrix * vPos;
		}   
    </script>

    <script type="text/x-glsl" id="fragment">
        varying vec3 vNormal;
        varying vec3 vTangent;
		varying vec3 vBitangent;
	    varying vec3 vPosition;
        varying vec2 vertexUV;

        uniform vec3 pointLightPosition; // in world space
		uniform vec3 clight;
		uniform sampler2D specularMap;
		uniform sampler2D diffuseMap;
        uniform sampler2D rougMap;
        uniform sampler2D normalMap;

        const float PI = 3.14159;
		vec3 cdiff;
		vec3 cspec;
        float roughness;

        vec3 FSchlick(float lDoth) {
				return (cspec + (vec3(1.0)-cspec)*pow(1.0 - lDoth,5.0));
		}

		float DGGX(float nDoth, float alpha) {
			float alpha2 = alpha*alpha;
			float d = nDoth*nDoth*(alpha2-1.0)+1.0;
			return (  alpha2 / (PI*d*d));
		}

		float G1(float dotProduct, float k) {
			return (dotProduct / (dotProduct*(1.0-k) + k) );
		}

		float GSmith(float nDotv, float nDotl) {
				float k = roughness*roughness;
				return G1(nDotl,k)*G1(nDotv,k);
		}

		void main() {
            vec4 lPosition = viewMatrix * vec4( pointLightPosition, 1.0 );
			vec3 l = normalize(lPosition.xyz - vPosition.xyz);

            vec3 normal = normalize( vNormal );
			vec3 tangent = normalize( vTangent );
			vec3 bitangent = normalize( vBitangent );
			mat3 vTBN = mat3( tangent, bitangent, normal );
			vec3 mapN = texture2D( normalMap, vertexUV ).xyz * 2.0 - 1.0;

            vec3 n = normalize( vTBN * mapN );
			//vec3 n = normalize( vNormal );  
			vec3 v = normalize( -vPosition);
			vec3 h = normalize( v + l);
			// small quantity to prevent divisions by 0
			float nDotl = max(dot( n, l ),0.000001);
			float lDoth = max(dot( l, h ),0.000001);
			float nDoth = max(dot( n, h ),0.000001);
			float vDoth = max(dot( v, h ),0.000001);
			float nDotv = max(dot( n, v ),0.000001);

            cdiff = texture2D( diffuseMap, vertexUV).rgb;
			cdiff = pow( cdiff, vec3(2.2));
			cspec = texture2D( specularMap, vertexUV ).rgb;
			cspec = pow( cspec, vec3(2.2));
			roughness = texture2D( rougMap, vertexUV).r;

			vec3 fresnel = FSchlick(lDoth);
			vec3 BRDF = (vec3(1.0)-fresnel)*cdiff/PI + fresnel*GSmith(nDotv,nDotl)*DGGX(nDoth,roughness*roughness)/
				(4.0*nDotl*nDotv);
			vec3 outRadiance = PI* clight * nDotl * BRDF;
			gl_FragColor = vec4(pow( outRadiance, vec3(1.0/2.2)), 1.0);
		}
    </script>
        
    <script src="poke.js"></script>
</body>

</html>